rootProject.name = 'file-management-test'

def file(dir, name) {
    return file("${dir.absolutePath}/${name}")
}

def GRADLE_TEXT = """

dependencies {
    
}
"""

["jy", "test", "web", "server"].forEach(moduleName -> {
    def moduleDir = file(rootDir, moduleName)

    if (!moduleDir.exists()) {
        moduleDir.mkdirs()
    }

    // moduleDir 아래에는 생성할 module directory를 직접 추가

    moduleDir.eachFile { subProjectDir ->
        def buildFile = file(subProjectDir, "build.gradle")

        if (!buildFile.exists()) {
            buildFile.text = GRADLE_TEXT
        }

        def projectName = ":${moduleName}-${subProjectDir.name}"
        include projectName
        project(projectName).projectDir = subProjectDir

        [
                "src/main/java/com/jy/unitt",
                "src/main/resources",
                "src/test/java/com/jy/unitt",
                "src/test/resources"
        ].forEach( src -> {
            def srcDir = file(subProjectDir, src)

            if (!srcDir.exists()) {
                srcDir.mkdirs()
            }
        })
    }
})